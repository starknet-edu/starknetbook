[id="starknet_contracts"]

= Starknet Contracts


"Starknet is a permissionless decentralized ZK-Rollup operating as an L2 network over Ethereum, where any dApp can achieve unlimited scale for its computation without compromising Ethereum's composability and security."
-- https://starknet.io/docs/hello_starknet/index.html#hello-starknet[Starknet Documentation].

Contracts on Starknet are written in Cairo. You can transpile a Solidity contract to Cairo via https://github.com/NethermindEth/warp[Warp] or use EVM bytecode using the Kakarot ZKEVM. However, the real power of Starknet will be harnessed by contracts written in Cairo itself.

We will create an application that allows users to vote with a yes (1) or no (0). The original concept was proposed by https://github.com/software-mansion-labs/protostar-tutorial[SWMansion] and was extensively modified for this tutorial.
For didactic purposes, we are going to show the contract code in parts;
the full contract is in link:./contracts/cairo/voting.cairo[this repository].
You can view and interact with the https://goerli.voyager.online/contract/0x01ab2d43fd8fe66f656aafb740f6a368cecb332b5e4e9bbc1983680a17971711[contract deployed on Voyager or StarkScan].
At the end of this tutorial, you will have your contract deployed.

== Introduction to Starknet accounts

Before coding our contract, let us create our Starknet wallets.
Install either or both, the https://chrome.google.com/webstore/detail/argent-x/dlcobpjiigpikoobohmabehhmhfoodbb[Argent X] or https://chrome.google.com/webstore/detail/braavos-wallet/jnlgamecbpmbajjfhmmmlhejkemejdma[Braavos] wallets, both are great and keep innovating at the threshold of Ethereum.
It is worth mentioning that both companies have extensively collaborated and pushed the Starknet ecosystem and are an example of the developer community.
Follow the instructions to deploy your Starknet account contract.

During these tutorials, we will use the Testnet, also called Starknet Goerli, SN Goerli, or Alpha-Goerli.
We will not need to spend actual funds while we try our contracts.
Make sure to set up your wallet to the Testnet;
the screenshot below shows how it looks for Braavos.

image::braavos1.png[braavos1]

You will need to fund your account with Goerli ETH.
You can get ETH using the https://faucet.goerli.starknet.io/[Starknet Goerli Faucet].
Just indicate the address of your account to get your funds.
You will likely need to wait about 5 minutes to notice the change in your wallet.

You will notice that these two wallets mention setting up your account and deploying your account, either to the testnet or the Mainnet.
Starknet leverages the Cairo Virtual Machine (CairoVM), which is not limited by the Ethereum Virtual Machine (in a later Camp, we will explore it deeper).
This means:

* Lessons learned from the development of the EVM can be applied, e.g., transaction parallelization.
* Key innovations, like Account Abstraction (Camp 3), that still need to be applied to the Layer 1 can be set up in Starknet.
* A different smart contract language can be leveraged to operate with the CairoVM.
This does not mean that EVM bytecode can not be read by Starknet (see the community-driven https://twitter.com/KakarotZkEvm[ZKEVM Kakarot] on top of Starknet).

In particular, due to Account Abstraction, Externally Owned Accounts (EOAs) in Ethereum L1 do not exist in Starknet.
EOAs have long been a discussion topic due to their limiting properties (see https://www.argent.xyz/blog/wtf-is-account-abstraction/[Argent's Account Abstraction] series).
The Starknet community has decided to move forward with Account Abstraction at a protocol level;
there is no differentiation between smart contracts, and even accounts are smart contracts that should be deployed.
This is why Argent and Braavos ask you to wait while they deploy your account contract.
We will get deeper into everything this allows us in Camp 3.

== Structure of a project on Starknet

We will create a "standard" Cairo code.
That is, following the smart contract programming conventions in Cairo.
Everything is still very new, but the conventions have adopted a style similar to Solidity.
For example, we will write our entire contract in a file called `voting.cairo`.
Therefore the path for our contract is link:./contracts/cairo/voting.cairo[`src/voting.cairo`].

Within our contract, we will follow the following structure, inspired by Solidity and suggested by https://hackmd.io/@0xHyoga/Skj7GGeyj[0xHyoga]:

. Structs
. Events
. Storage variables
. Constructor
. Storage Getters
. Constant functions: functions that do not change the state
. Non-Constant functions: functions that change the state

We still need to find out what these elements are but don't worry, we will learn about them.
If you have a background in contract creation, you will see the similarities with Solidity.
It is enough to know that we will follow this order in our contract.

== Let's vote!

Let's start with the following code (remember that the full contract is in link:./contracts/cairo/voting.cairo[this repository].).
We create two `struct`.
The first, `VoteCounting`, will keep track of the votes: the number of votes with "`yes`" and the number of votes with "`no`".
The second, `VoterInfo`, indicates whether a voter is allowed to vote.
Then we will create storage variables (we will see them later).

* `voting_status` keeps and updates the vote count.
* `voter_info` indicates and updates a voter's information (may or may not vote).
* `registered_voter` indicates if an address is allowed to vote.

[,cairo]
----
%lang starknet

from starkware.cairo.common.cairo_builtins import HashBuiltin
from starkware.starknet.common.syscalls import get_caller_address
from starkware.cairo.common.math import assert_not_zero

from openzeppelin.access.ownable.library import Ownable
from openzeppelin.security.pausable.library import Pausable

// ------
// Structs
// ------

// struct that carries the status of the vote
struct VoteCounting {
    votes_yes : felt,
    votes_no : felt,
}

// struct indicating whether a voter is allowed to vote
struct VoterInfo {
    allowed : felt,
}

// ------
// Storage
// ------

// storage variable that takes no arguments and returns the current status of the vote
@storage_var
func voting_status() -> (res : VoteCounting) {
}

// storage variable that receives an address and returns the information of that voter
@storage_var
func voter_info(user_address: felt) -> (res : VoterInfo) {
}

// storage variable that receives an address and returns if an address is registered as a voter
@storage_var
func registered_voter(address: felt) -> (is_registered: felt) {
}
----

We go step by step noting the differences with the programs in Cairo that we have created before.

We have to declare that we want to deploy our contract on Starknet.
At the beginning of the contract, write `%lang starknet`.
Unlike the Cairo contracts, which are not deployed on Starknet, we don't have to mention the `builtins` at the beginning of the contract.

We will learn about various Starknet primitives (marked with an `@`, like Python decorators) that don't exist in pure Cairo.
The first is `@storage_var`.

== Contract storage

What is the storage space of a contract?
Let's look at the documentation:

"The storage space of the contract is a persistent storage space where data can be read, written, modified, and preserved. Storage is a map with 2{caret}\{251} slots, where each slot is a felt that is initialized to 0." 
-- Starknet documentation.

To interact with the contract's storage, we create storage variables.
We can think of storage variables as pairs of keys and values.
Think of the concept of a dictionary in Python, for example.
We are mapping a key with, possibly several, values.

The most common way to interact with the contract's storage and to create storage variables is with the decorator (yes, just like in Python) `@storage_var`.
The methods (functions) `.read(key)`, `.write(key, value)`, and `.addr(key)` are automatically created for the storage variable.
Let's do an example where we don't have a key, but we do have a value (`res`), above we create the storage variable `voting_status`:

[,cairo]
----
@storage_var
func voting_status() -> (res : VoteCounting) {
}
----

We can then get the value stored in `voting_status()`, a `VoteCounting` struct, with `let (status) = voting_status.read()`.
Note that we don't have to provide arguments to the read as `voting_status()` doesn't require it.
Also, note that `.read()` returns a tuple, so we have to receive it with a `let (variable_name) = â€¦` (we saw this in a previous tutorial).
It's the same with `voting_status.addr()`, which returns the address in the storage of the storage variable `voting_status`.
We do not indicate any argument because it is not required.

To write a new status to our storage variable, we use `voting_status.write(new_voting_count)`, where `new_voting_count` is a struct of type `VoteCounting`.
What we did was store the new struct inside the storage variable.

Let's look at a storage variable with a key (`user_address`) and a value (`res`).

[,cairo]
----
@storage_var
func voter_info(user_address: felt) -> (res : VoterInfo) {
}
----

With `let (caller_address) = get_caller_address()` and before `from starkware.starknet.common.syscalls import get_caller_address`, we can obtain the address of the account interacting with our contract.
We ask for caller information: `let (caller_info) = voter_info.read(caller_address)`.
If we did not put the caller's address, we would have obtained an error because `voter_info(user_address: felt)` requires a key in felt format, in this case, a contract address.
Note the difference with `voting_status()`, which did not require a key.

We can write with `voter_info.write(caller_address, new_voter_info)`, where `new_voter_info` is a struct of type `VoterInfo`.
Here we indicate that for the `caller_address`, we have a new `VoterInfo` called `new_voter_info`.
We can do the same with the following address.
With `voter_info.addr(caller_address)`, we get the address where the first element of the value is stored, in this case, `VoterInfo`.

We can also use the functions `storage_read(key)` and `storage_write(key, value)` (imported with `from starkware.starknet.common.syscalls import storage_read, storage_write`) to read the value(s) from a key and write a value(s) to a key, respectively.
Our `@storage_value` decorator uses the functions below.

== The three most used implicit arguments

Let's move on to the following code snippet of our voting contract (you can find the commented code in the link:./contracts/cairo/voting.cairo[tutorial repository]).
This section will cover three of the most common implied arguments in Starknet contracts.
All three are widely used because they are required by the storage variables to write to and read from the contract's storage space.

We create the inner function `_register_voters` (by default, all functions in Starknet are private, unlike Solidity).
With it, we will prepare our list of voters.
We assume we have a list of addresses allowed to vote.
`_register_voters` uses the storage variable `voter_info` to assign each address its voting status: whether it is allowed to vote.

[,cairo]
----
func _register_voters {
    syscall_ptr : felt*,
    pedersen_ptr : HashBuiltin*,
    range_check_ptr,
    }(registered_addresses_len: felt, registered_addresses : felt*){

    // No more voters, recursion ends
    if (registered_addresses_len == 0){
        return ();
    }

    // Assign the voter at address 'registered_addresses[registered_addresses_len - 1]' a VoterInfo struct
    // indicating that they have not yet voted and can do so
    let votante_info = VoterInfo(
        allowed=1,
    );
    registered_voter.write(registered_addresses[registered_addresses_len - 1], 1);
    voter_info.write(registered_addresses[registered_addresses_len - 1], votante_info);

    // Go to next voter, we use recursion
    return _register_voters(registered_addresses_len - 1, registered_addresses);
}
----

We notice the use of three implicit arguments we had not seen before: `syscall_ptr : felt*`, `pedersen_ptr : HashBuiltin*`, and `range_check_ptr`.
All three are pointers;
note that they end their name with a `_ptr`.

`syscall_ptr` is used when we make system calls.
We include it in `_register_voters` because `write` and `read` need this implicit argument.
When reading and writing, we are directly consulting the contract's storage, and in Starknet, this is achieved by making system calls.
It is a pointer to a felt value, `felt*`.

`range_check_ptr` allows comparing integer numbers. In a subsequent Camp, we will take a closer look at pointers and essential builtin functions in Starknet development. For now, it is enough for us to know that the `write` and `read` arguments of the storage variables need to compare numbers; therefore, we need to indicate the implicit `range_check_ptr` argument in any function that reads and writes to the contract's storage using storage variables.


This is an excellent time to introduce hashes:

"A hash is a mathematical function that converts an arbitrary length input into an encrypted output of fixed length. So regardless of the original amount of data or the file size involved, your unique hash will always be the same size. Also, hashes cannot be used to "reverse engineer" the input from the hash output since hash functions are "unidirectionals" (like a meat grinder; you can't put ground beef back into a steak)." 
-- Investopedia.

Along with Starknet Keccak (the first 250 bits of the Keccak256 hash), the Pedersen hash function is one of two hash functions used on Starknet.
`pedersen_ptr` is used when running a Pedersen hash function.
We put this pointer in `_register_voters` because storage variables perform a Pedersen hash to calculate their memory address.

The implicit argument `pedersen_ptr` is a pointer to a HashBuiltin struct defined in the https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/cairo_builtins.cairo[Cairo common library]:

[,cairo]
----
struct HashBuiltin {
    x: felt,
    y: felt,
    result: felt,
}
----

== Handling errors in Cairo

Inside a function, we can mark an error in the contract if a condition is false.
For example, the error would be raised in the following code because `assert_nn(amount)` is false (`assert_nn` checks if a value is non-negative).
If `amount` were ten, then `assert_nn(amount)` would be valid, and the error would not be raised.

[,cairo]
----
let amount = -10

with_attr error_message("Quantity should be positive. You have: {amount}.") {
  assert_nn(amount)
}
----

We will create a function, `_assert_allowed`, which will check if a specific voter is allowed to vote, and if not, it will return an error.

[,cairo]
----
from starkware.cairo.common.math import assert_not_zero

...

func _assert_allowed {
    syscall_ptr : felt*,
    //pedersen_ptr : HashBuiltin*,
    range_check_ptr
}(info : VoterInfo) {

    with_attr error_message("VoterInfo: Your address is not allowed to vote."){
        assert_not_zero(info.allowed);
    }

    return ();
}
----

We import `assert_not_zero`.
The error will return a message if `assert_not_zero(info.allowed)` is false.
Remember that if a voter is allowed to vote, then `info.allowed` will be 1.

== External functions

Let's move on to the primary function of our Application.
We write a function that takes as an explicit argument a vote (1 or 0) and then updates the total vote count and the state of the voter so that they cannot vote again.

[,cairo]
----
%lang starknet
from starkware.cairo.common.cairo_builtins import HashBuiltin
from starkware.starknet.common.syscalls import get_caller_address

...

@external
func vote {
    syscall_ptr : felt*,
    pedersen_ptr : HashBuiltin*,
    range_check_ptr
}(vote : felt) -> () {
    alloc_locals;
    Pausable.assert_not_paused();

    // Know if a voter has already voted and continue if they have not voted
    let (caller) = get_caller_address();
    let (info) = voter_info.read(caller);
    _assert_allowed(info);

    // Mark that the voter has already voted and update the storage
    let updated_info = VoterInfo(
        allowed=0,
    );
    voter_info.write(caller, updated_info);

    // Update the vote count with the new vote
    let (status) = voting_status.read();
    local updated_voting_status : VoteCounting;
    if (vote == 0){
        assert updated_voting_status.votes_no = status.votes_no + 1;
        assert updated_voting_status.votes_yes = status.votes_yes;
    }
    if (vote == 1){
        assert updated_voting_status.votes_no = status.votes_no;
        assert updated_voting_status.votes_yes = status.votes_yes + 1;
    }
    voting_status.write(updated_voting_status);
    return ();
}
----

In the `common.syscalls` library (https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/starknet/common/syscalls.cairo[link to repo]) we found useful functions to interact with the system.
For example, above, we used `get_caller_address` to get the contract's address interacting with ours.
Other interesting functions are `get_block_number` (to get the number of the block) or `get_contract_address` to get the address of our contract.
Later we will use more functions of this library.

The following new thing is the `@external` decorator used on the `vote` function.
Note that we haven't created any `main` functions as we did with simple Cairo.
It's because Starknet doesn't use the `main` function!
Here we use `external` and `view` functions to interact with the contracts.

*External functions*.
Using the `@external` decorator, we define a function as external.
Other contracts (including accounts) can interact with external functions, read, and write.
For example, our `vote` function can be called by other contracts to cast a vote of 1 or 0;
then, `vote` will write to the contract storage.
For example, with `voter_info.write(caller, updated_info)` we are writing to the storage variable `voter_info`.
That is, we are modifying the state of the contract.
Here's the key difference from the `view` functions (we'll get to that later): external functions change the contract's status in writing power.

== Getter functions (view functions)

Let's write functions that allow other contracts (including accounts) to check the status of the current vote.
These functions that enable you to check the state are called `getters`.
First, we create a getter, `get_voting_status`, which returns the current status of the vote;
that is, it returns a struct `VoteCounting` with the total vote count.
Next, we create the getter `get_voter_status`, which returns the status of a particular address (voter) (whether they have already voted or not).
Review the link:./contracts/cairo/voting.cairo[final contract] to see other added getter functions.

[,cairo]
----
%lang starknet
from starkware.cairo.common.cairo_builtins import HashBuiltin

...

@view
func get_voting_status {
    syscall_ptr : felt*,
    pedersen_ptr : HashBuiltin*,
    range_check_ptr
}() -> (status: VoteCounting) {
    let (status) = voting_status.read();
    return (status = status);
}


@view
func get_voter_status {
    syscall_ptr : felt*,
    pedersen_ptr : HashBuiltin*,
    range_check_ptr
}(user_address: felt) -> (status: VoterInfo) {
    let (status) = voter_info.read(user_address);
    return(status = status);
}
----

*View functions.* Using the `@view` decorator, we define a function as view.
Other contracts (including accounts) can read from the contract status;
they cannot modify it (note that externals can modify it).
Reading from storage does not cost gas!

Note that in Solidity, the compiler creates getters for all state variables declared as public;
in Cairo, all storage variables are private.
Therefore, if we want to make the storage variables public, we must make a getter function ourselves.

== Constructors

Constructor functions are used to initialize a Starknet Application.
We define them with the `@constructor` decorator.
It receives the inputs our contract needs to be deployed and performs the necessary operations to start operating with the contract.
For example, our contract needs a voting administrator and a list of addresses that can vote (not everyone can vote for a president).
All our Application mechanism is ready;
it only needs to be given the inputs required to start working.

Beware, Cairo only supports *1 constructor per contract*.

[,cairo]
----
from openzeppelin.access.ownable.library import Ownable

...

@constructor
func constructor{syscall_ptr: felt*, pedersen_ptr: HashBuiltin*, range_check_ptr}(
    admin_address: felt, registered_addresses_len: felt, registered_addresses: felt*
) {
    alloc_locals;
    Ownable.initializer(admin_address);
    _register_voters(registered_addresses_len, registered_addresses);
    return ();
}
----

In the constructor, we are indicating that we require three inputs to initialize the contract:

* `admin_address: felt` - The address of the voting administrator contract.
This contract may, for example, pause voting if necessary.
You can add it in hex or felt format (and it will be converted to felt anyway).
* `registered_addresses_len: felt` - This value is the length of the array of addresses that can vote.
For example, if ten addresses can participate in the vote, it will be 10.
* `registered_addresses: felt*` - An array with the addresses that can vote.
Arrays are entered one after the other without a comma, for example, `0x02cdAb749380950e7a7c0deFf5ea8eDD716fEb3a2952aDd4E5659655077B8510 0x02cdAb749380950e7a7c0deFf5ea8eDD716fEb3a2952aDd4E5659655077B8512`.

In your terminal, compile with:

----
cairo-compile contracts/cairo/voting.cairo \
    --output contracts/cairo/voting_compiled.json
----

This command will create the `voting_compiled.json` file, which contains the contract's bytecode.
We will use it to declare our contract and then deploy it.

In the next section we will learn how to deploy Starknet smart contracts.