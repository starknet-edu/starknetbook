[id="testing_protostar"]

= Testing contracts with Protostar

"Protostar provides an environment to write unit tests for Starknet smart contracts in *Cairo language itself*. With the help of cheat codes like `deploy_contract`, it is also easy to write integration tests, deploy single or multiple contracts and examine the internal behavior of such small systems" 
-- (https://docs.swmansion.com/protostar/docs/tutorials/testing/e2e[Protostar documentation])

In other words, thanks to Protostar cheat codes (a term adopted from Foundry), we can create both unit and integration tests. For integration tests, we would need more than assertions; we would need to manipulate the state of the blockchain, check for reversals and events, change our identity, and more. For this reason, Protostar provides cheat codes (https://docs.swmansion.com/protostar/docs/tutorials/testing/cheatcodes[Protostar documentation], https://book.getfoundry.sh/forge/cheatcodes[Foundry documentation]). The cheat codes are implemented using python hints within our tests.

Protostar divides our tests into test suites, each with test cases. A suite is a Cairo file (remember, Protostar allows you to code tests using Cairo) with external functions (the test cases) that use assertions and cheat codes to test our contract. If we are testing two different contracts, we can create two separate test suites. If we are trying two different flows in our contract, we can create two test cases for each flow.

To run all the test suites in the `tests` directory of a project, with the Open Zeppelin cairo-contracts dependency installed, we can run the following:

[,Bash]
----
protostar test ./tests \
    --cairo-path lib/cairo_contracts/src/
----

== Testing the voting contract

This section will code the unit and integration tests for the voting contract. The goal is to introduce some of the most critical testing tools that Protostar provides. The https://docs.swmansion.com/protostar/docs/tutorials/testing[Protostar documentation] contains additional tools for testing.

We will learn the following:

. How to deploy a contract inside our test.
. How to interact with the deployed contracts.
. How to use cheat codes to simulate behaviors.

We can find the complete set of tests for our voting contract at link:./tests/test_vote.cairo[tests/test_vote.cairo]. Here we will dissect the most important parts of it.

Due to the large number of `view` and `external` functions, our voting contract depends on interaction with the state of the blockchain; Therefore, we need to interact with the deployed contract and rely on integration tests.

The first thing we need to do for our integration tests is deploy the contract. We want to do this at the beginning of our test suite, so we don't have to deploy it in every test case. Protostar allows us to do this with configuration hooks. Setting up our configuration hooks is the first thing we do in our link:./tests/test_vote.cairo[test suite]. We deploy our contract by:

* declaring what contract we want to deploy;
* preparing it by defining the call data for the constructor; and
* deploying it.

Additionally, we want to store the address of our deployed contract in a variable that we can call through the test suite. We can use the context variable to pass data from the config hook to the test cases (functions); in this case, we are transmitting the address or the deployed contract.

[,Cairo]
----
@external
func __setup__() {
    %{
        declared = declare("src/voting.cairo")
        prepared = prepare(declared, [ids.ADMIN, ids.NUMBER_VOTERS, ids.VOTER_1, ids.VOTER_2, ids.VOTER_3])
        context.context_vote_address = deploy(prepared).contract_address
    %}
    return ();
}
----

To interact with our contract, it is helpful to represent its functionality through an Interface. Interfaces on Starknet are `namespaces` preceded by the `@contract_interface` entry point (decorator) that can:

* ... not have any function with implementation.
* ... not have a constructor.
* ... not have state variables.
* ... can have ``struct``s

We add all the `view` and `external` functions from our contract to the Interface at link:/src/interfaces/IVote.cairo[IVote.cairo]. We can call our entire contract functionality through the Interface. For example, here we call the function `is_voter_registered`:

[,Cairo]
----
IVote.is_voter_registered(contract_address=vote_address, address=VOTER_1)
----

Note that an Interface automatically adds a new parameter, `contract_address`, where we need to add the address of the deployed contract that contains the actual functionality we want to emulate. Then we write the other required argument of the function we call, in this case, `address`. We will use the `IVote` Interface when testing our contract.

Once the contract is deployed, testing the initial state of view functions is easy and requires no cheat codes. In the following snippet, we test that our contract deployment was correct, i.e., that the call data added to the constructor is reflected via the `view` functions.

[,Cairo]
----
@external
func test_vote_contract_deploy{syscall_ptr: felt*, range_check_ptr}() {

    tempvar vote_address: felt;

    %{
        ids.vote_address = context.context_vote_address
    %}

    //test the admin view function
    let (admin) = IVote.admin(contract_address=vote_address);
    assert ADMIN = admin;

    // Test the is_voter_registered view function; where the voters registered?
    let (voter_1) = IVote.is_voter_registered(contract_address=vote_address, address=VOTER_1);
    assert 1 = voter_1;
    let (voter_2) = IVote.is_voter_registered(contract_address=vote_address, address=VOTER_2);
    assert 1 = voter_2;
    let (voter_3) = IVote.is_voter_registered(contract_address=vote_address, address=VOTER_3);
    assert 1 = voter_2;

    %{
        print("üê∫ Successful deployment of the contract; constructor successfully implemented")
    %}

    return ();
}
----

When we deployed our contract, we stored the contract's address in the `context.context_vote_address` variable. We retrieve the address and store it in the Cairo `tempvar` `vote_address`.

[,Cairo]
----
tempvar vote_address: felt;

    %{
        ids.vote_address = context.context_vote_address
    %}
----

With the address, we can call the Interface `IVote` to test the contract's functions.

We make much use of `assert` to review that the values are correct.


(As we reviewed in Camp 1) The `assert` statement in Cairo can assert that the value at a given memory location equals something. However, it can also set the value if the given memory location is still unset. When you're writing tests, you want the test to fail in the second case. To do so, a simple tip is to write Yoda-style assertions (https://mirror.xyz/onlydust.eth/uhKk_3p34mE0oFUxkIYlCsjkE7ZvUfSFWU83UM9_w-w[Only Dust]).

That is why we write:

[,Cairo]
----
assert 1 = voter_1;
----

We did not write it:

[,Cairo]
----
assert voter_1 = 1;
----

If we would like to execute our tests frequently, we want to make them fast. The most efficient way to do so is to execute only the test we are interested in (https://mirror.xyz/onlydust.eth/uhKk_3p34mE0oFUxkIYlCsjkE7ZvUfSFWU83UM9_w-w[Only Dust]). To run this specific test case, we can call in the terminal:

[,Bash]
----
protostar test tests/test_vote.cairo::test_vote_contract_deploy --cairo-path lib/cairo_contracts/src
----

== Pranking and testing errors

So far, we only manipulated the state of the blockchain when we deployed the contract. We must trick the blockchain by interacting with it from different accounts (identities) and altering the state.

The `start_prank` cheat code allows us to "prank" the blockchain with our identity. We can trick it into thinking that we are calling it from one or another account. In the `test_vote_contract_vote` test case, we simulate votes from the registered addresses. `start_prank` takes two arguments (review the https://docs.swmansion.com/protostar/docs/tutorials/testing/cheatcodes/start-prank[Protostar documentation]): the address of the account that would be making the call/invocation, and the address of the contract we are calling.

For example, here we are invoking the `vote` function of the `IVote` Interface from the `VOTER_1` account. In other words, we are simulating a vote, 0 (no), from `VOTER_1`.

[,Cairo]
----
%{ stop_prank_voter = start_prank(ids.VOTER_1, ids.vote_address) %}
IVote.vote(contract_address=vote_address, vote=0);
%{ stop_prank_voter() %}
----

We can then test if our vote indeed altered the blockchain state (review the full link:/src/interfaces/IVote.cairo[test suite]).

In the `test_vote_contract_pause_vote` test case, we use `start_prank` to pause voting; we pretended that our identity was that of the voting administrator:

[,Cairo]
----
%{ stop_prank_admin = start_prank(ids.ADMIN, ids.vote_address) %}
IVote.pause(contract_address=vote_address);
%{ stop_prank_admin() %}
----

We also want to test that our error messages and exceptions are triggered at the right time. When we built our voting smart contract in Camp 1, we defined that an error, `"VoterInfo: Your address does not have permission to vote"`, would appear when an address does not have permission to vote. In the `test_vote_contract_vote` case, we simulate a vote by VOTER_1, so they are no longer allowed to vote. We want to test if this error appears at the right time; for this, we use the `expect_revert` cheat code. A test will pass if a code below `expect_revert` raises a specific exception. Otherwise, the test will fail (https://docs.swmansion.com/protostar/docs/tutorials/testing/cheatcodes/expect-revert[Protostar documentation]).

[,Cairo]
----
%{ expect_revert(error_message="not allowed to vote") %}
%{ stop_prank_voter = start_prank(ids.VOTER_1, ids.vote_address) %}
IVote.vote(contract_address=vote_address, vote=0);
%{ stop_prank_voter() %}
----

There are many https://docs.swmansion.com/protostar/docs/tutorials/testing/cheatcodes[other cheat codes in Protostar], and some of them may be useful for your particular case; don stop exploring

The testing process allowed us to move more confidently to the development network or test network for further testing or interacting with the first users; we would like to make sure that the interactions with the network are correct, for example, we would like to use private keys for our accounts, make sure that the nonces are correct, that the front-end can connect correctly with the contract, etc.

We would need additional tools to continue testing the integration of our contracts with the ourside world. https://docs.swmansion.com/protostar/docs/tutorials/testing/e2e[These tools] are recommended by the Protostar documentation:

. The Devnet for running a local Starknet network,
. Starknet SDKs like https://www.starknetjs.com/[starknet.js], https://starknetpy.rtfd.io/[starknet.py], or https://github.com/xJonathanLEI/starknet-rs[starknet-rs] for interacting with Starknet from tests' code,
. Protostar CLI for building contracts and deploying in tests' setup phases and CI.

== Contributing 

üéØ +++<strong>+++Camper: +++</strong>+++ This book is an open source effort, made possible only by contributions from readers like you. If you are interested in making this resource better for other users - please suggest a change by following the instructions https://github.com/starknet-edu/basecamp/blob/antora-front/CONTRIBUTING.adoc[here].