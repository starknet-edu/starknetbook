[id="builtin"]

= Deep into Builtins

The Algebraic RISC can simulate any Turing Machine and hence is Turing-complete (supports any feasible computation).
However, implementing some basic operations, such as the comparison of elements, using only Cairo instructions would result in a lot of steps which damages the goal of minimizing the number of trace cells used.
Consider that adding a new instruction to the Instruction Set has a cost even if this instruction is not used.
To mitigate this without increasing the number of trace cells per instruction, Cairo introduces the notion of builtins.

____
Builtins are predefined optimized low-level execution units which are added to the Cairo CPU board to perform predefined computations which are expensive to perform in vanilla Cairo (e.g., range-checks, Pedersen hash, ECDSA, ...).
____

The communication between the CPU and the builtins is done through memory: each builtin is assigned a continuous area in the memory and applies some constraints (depending on the builtin definition) on the memory cells in that area.
In terms of building the AIR, it means that adding builtins does not affect the CPU constraints.
It just means that the same memory is shared between the CPU and the builtins.
To "`invoke`" a builtin, the Cairo program "`communicates`" with certain memory cells, and the builtin enforces some constraints on those memory cells.

For example, the `range-check` builtin enforces that all the values for the memory cells in some fixed address range are within the range $[0, 2{caret}\{128})$.
The memory cells constrained by the `range-check` builtin are called "range-checked" cells.

In practical terms, a builtin is utilized by writing (and reading) the inputs to a dedicated memory segment accessed via the "builtin pointer": each builtin has its pointer to access the builtin's memory segment.
Builtin pointers follow the name convention `<builtin name>_ptr`;
e.g., `range_check_ptr`.
In the Cairo case, the builtin directive adds the builtin pointers as parameters to main which can then be passed to any function making use of them.
Builtin declarations appear at the top of the Cairo code file.
They are declared with the `%builtins` directive, followed by the name of the builtins;
e.g., `%builtin range_check`.
In Starknet contracts, it is not necessary to add them.

Builtin pointers can be of different types.
The following table summarizes the available builtins, what they are for, their pointer names, and their pointer types.

[cols="^,,^,^"]
|===
| *Builtin* | For... | *Pointer name* | *Pointer type*

| output
| Writing program output which appears explicitly in an execution proof
| output_ptr
| felt*

| pedersen
| Computing the Pedersen hash function
| pedersen_ptr
| HashBuiltin*

| range_check
| Checking that a field element is within a range $[0,2{caret}\{128})$, and for doing various comparisons
| range_check_ptr
| felt (not a pointer)

| ecdsa
| Verifying ECDSA signatures
| ecdsa_ptr
| SignatureBuiltin*

| bitwise
| Performing bitwise operations on felts
| bitwise_ptr
| BitwiseBuiltin*
|===

The details of each type are in the https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/cairo_builtins.cairo[common library].
Each type, that is not directly a `felt*`, is nothing more than a struct.
For example, the https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/cairo_builtins.cairo#L5[`HashBuiltin`]:

[,Rust]
----
struct HashBuiltin {
    x: felt,
    y: felt,
    result: felt,
}
----

Note the following implementation of the https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/hash.cairo[hash2 function] to compute the hash of two given field elements:

[,Rust]
----
from starkware.cairo.common.cairo_builtins import HashBuiltin

func hash2(hash_ptr: HashBuiltin*, x, y) -> (
    hash_ptr: HashBuiltin*, z: felt
) {
    let hash = hash_ptr;
    // Invoke the hash function.
    hash.x = x;
    hash.y = y;
    // Return the updated pointer (increased by 3 memory cells)
    // and the result of the hash.
    return (hash_ptr=hash_ptr + HashBuiltin.SIZE, z=hash.result);
}
----

`hash_ptr` was added as an explicit argument and explicitly returned updated (`hash_ptr + HashBuiltin.SIZE`).
We have to keep track of a pointer to the next unused builtin memory cell.
The convention is that functions that use the builtin should get that pointer as an argument and return an updated pointer to the next unused builtin memory cell.

It is easier to use implicit arguments: a Cairo syntactic sugar that automatically returns the implicit argument.
The common library implementation of the https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/hash.cairo[hash2 function] is:

[,Rust]
----
%builtins pedersen

from starkware.cairo.common.cairo_builtins import HashBuiltin

func hash2{hash_ptr: HashBuiltin*}(x, y) -> (result: felt) {
    // Invoke the hash function
    hash_ptr.x = x;
    hash_ptr.y = y;
    let result = hash_ptr.result;
    // Update hash_ptr (increased by 3 memory cells)
    let hash_ptr = hash_ptr + HashBuiltin.SIZE;
    return (result=result);
}
----

`hash_ptr` is updated but this time it was returned implicitly.
As another example, this is the implementation of the https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/serialize.cairo#L1[serialize_word function] which will output a felt into your terminal:

[,Rust]
----
func serialize_word{output_ptr: felt*}(word) {
    // Invoke the output function
    assert [output_ptr] = word;
    // Update output_ptr (increased by 1 memory cell)
    let output_ptr = output_ptr + 1;
    return ();
}
----

Refer to the implementation in  https://github.com/starknet-edu/starknetbook/blob/main/chapters/modules/chapter_5/pages/builtins/cairo/hash.cairo[builtins/cairo/hash.cairo] for an example using the `output`, `pedersen`, and `range_check` builtins.

At what moment did the `hash2` and `serialize_word` functions invoked the properties of their builtins?

. When we called `%builtins output pedersen` at the start of the Cairo program, the Cairo VM prepares to use the `output_ptr` and `pedersen_ptr` pointers, and their respective memory segments: usually 2 and 3 (segment 1 is commonly the execution segment, more on the next section).
. Second, the Cairo code reads or writes to the memory cells in the segments assigned to the `output` and `pedersen`.
. Finally, when a value is written in a memory cell inside a builtin segment, the builtin properties are invoked.
For example, when a `struct HashBuiltin` is defined (you have to indicate `x` and `y`, see the structure of https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/cairo_builtins.cairo#L5[`HashBuiltin`]) then the Pedersen builtin enforces that $result == hash(x, y)$.
We can then retrieve the value of `result` with `let result = hash_ptr.result;` as in the `hash2` function.
Whenever the program wishes to communicate information to the verifier, it can do so by writing it to a designated memory segment which can be accessed by using the `output` builtin (see more in https://starknet.io/docs/how_cairo_works/program_input_and_output.html#id2[the docs]).



