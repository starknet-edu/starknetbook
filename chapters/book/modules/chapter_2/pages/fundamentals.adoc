[id="structure"]

= Fundamentals of Cairo for Starknet

In this chapter, we'll review the features of the Cairo language as it pertains to Starknet, a Layer 2 solution for Ethereum. We will use a series of practical smart contract examples from voting systems to Pokemon contracts.

For those seeking a more in-depth exploration of Cairo, outside its Starknet application, the **https://cairo-book.github.io/[Cairo Book]** and **https://github.com/shramee/starklings-cairo1[Starklings]** are your go-to resources. We also recommend you finish the https://github.com/starknet-edu/starknet-cairo-101[*starknet-cairo-101*] automated workshop to get a better understanding of the structure of a smart contract, however, neither of these resources are required to understand the content of the following subchapters or the Starknet Book in general.

Our journey begins with a 'Vote' contract. The Vote contract represents a straightforward demonstration of the foundational elements of a Cairo contract. It serves as an on-chain voting system, enabling a pre-defined set of voters to cast votes ('1' for 'Yes' and '0' for 'No') on a given proposal.

Primarily, the Vote contract maintains a record of the count of 'Yes' and 'No' votes and offers functions that read and write from the contract's state. These functions are crucial for examining the voting status and voter eligibility. The contract is initialized with three registered voters and is deployed on the Starknet testnet.

== Structure of a Cairo Contract

A Cairo contract is made up of several sections, which are outlined and discussed in the Vote contract:

* Imports: These represent the necessary libraries and dependencies required for the contract's operation.
* Trait Interfaces: Defines the functions that can be implemented or called by the Cairo contract. They are characterized by the `#[starknet::interface]` attribute.
* Contract Definition: The core part of the contract where the contract's functionalities are defined. It's marked with the `#[starknet::contract]` attribute.
* Storage: It represents the state variables that the contract uses to store data.
* Constructor: The function that sets the initial state of the contract.
* Functions: These are categorically divided into read functions (which only fetch data from the contract's state) and write functions (which can alter the state of the contract). Both types of functions can be external (callable from outside the contract) or internal (used within the contract). 
* Event: An event is a way to notify the outside world about the contract's state changes. It's marked with the `#[event]` attribute.

== Starknet Modules

The **`mod`** keyword is used to define a module in Cairo, and modules provide a way to organize code into namespaces. In the context of Starknet, a module represents a smart contract.

To declare a Starknet contract, the **`mod`** keyword is used in combination with the **`#[starknet::contract]`** attribute. The **`#[starknet::contract]`** attribute informs the compiler that the following module should be compiled as a Starknet contract.

For example, here's how we declare a Starknet contract:

```
#[starknet::contract]
mod Vote {
    // contract definition goes here
}
```

In this example, **`Vote`** is the name of the Starknet contract. All the contract's storage variables, functions (constructor, external and internal), and any associated `impl` are defined within this module.

The contract can then be deployed to the Starknet network, where it will have its own unique contract address and state, and its functions can be called by users and other contracts in the network.

=== Functions 

Functions in Cairo contracts are defined within implementation blocks (`impls`), except for the constructor (more in following subchapters):

* External functions are prefixed with `#[external(v0)]` and defined in an `impl` block for the interface trait (`VoteTrait` in our example). These functions can be called from outside the contract and can modify the contract's state if necessary.
* Internal functions are defined in `impl` blocks marked with `#[generate_trait]`. These functions are helper functions and can only be called by other functions within the contract.

The Vote contract provides an example of these principles, featuring a clear division between functions that read data (`get_vote_status`, `voter_can_vote`, `is_voter_registered`) and those that write data (`vote`). 

Note: The `VoteTrait` in our example outlines the primary interface of the Vote contract, providing a blueprint for the external functions that the contract can perform.

Here is the Vote contract's code, however, we will break down the code in the following subchapters:

[source,rust]
----
/// The Contract Class Hash is 0x8873aa28af0a0e6ac6aa647aa8e8c02cea7752bb7950284dbbbae1be35e9cb
/// The contract is deployed on the Starknet testnet. The contract address is 0x056d42ddcc1c85959989aaef369e284804a8e59cc5ce519e579fcb121b18f724

/// @dev Core Library Imports for the Traits outside the Starknet Contract
use starknet::ContractAddress;

/// @dev Trait defining the functions that can be implemented or called by the Starknet Contract
#[starknet::interface]
trait VoteTrait<T> {
    /// @dev Function that returns the current vote status
    fn get_vote_status(self: @T) -> (u8, u8, u8, u8);

    /// @dev Function that checks if the user at the specified address is allowed to vote
    fn voter_can_vote(self: @T, user_address: ContractAddress) -> bool;

    /// @dev Function that checks if the specified address is registered as a voter
    fn is_voter_registered(self: @T, address: ContractAddress) -> bool;

    /// @dev Function that allows a user to vote
    fn vote(ref self: T, vote: u8);
}

/// @dev Starknet Contract allowing three registered voters to vote on a proposal
#[starknet::contract]
mod Vote {
    use starknet::ContractAddress;
    use starknet::get_caller_address;

    const YES: u8 = 1_u8;
    const NO: u8 = 0_u8;

    /// @dev Structure that stores vote counts and voter states
    #[storage]
    struct Storage {
        yes_votes: u8,
        no_votes: u8,
        can_vote: LegacyMap::<ContractAddress, bool>,
        registered_voter: LegacyMap::<ContractAddress, bool>,
    }

    /// @dev Contract constructor initializing the contract with a list of registered voters and 0 vote count
    #[constructor]
    fn constructor(
        ref self: ContractState,
        voter_1: ContractAddress,
        voter_2: ContractAddress,
        voter_3: ContractAddress
    ) {
        // Register all voters by calling the _register_voters function 
        self._register_voters(voter_1, voter_2, voter_3);

        // Initialize the vote count to 0
        self.yes_votes.write(0_u8);
        self.no_votes.write(0_u8);
    }

    /// @dev Event that gets emitted when a vote is cast
    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        VoteCast: VoteCast,
        UnauthorizedAttempt: UnauthorizedAttempt,
    }

    /// @dev Represents a vote that was cast
    #[derive(Drop, starknet::Event)]
    struct VoteCast {
        voter: ContractAddress,
        vote: u8,
    }

    /// @dev Represents an unauthorized attempt to vote
    #[derive(Drop, starknet::Event)]
    struct UnauthorizedAttempt {
        unauthorized_address: ContractAddress,
    }

    /// @dev Implementation of VoteTrait for ContractState
    #[external(v0)]
    impl VoteImpl of super::VoteTrait<ContractState> {
        /// @dev Returns the voting results
        fn get_vote_status(self: @ContractState) -> (u8, u8, u8, u8) {
            let (n_yes, n_no) = self._get_voting_result();
            let (yes_percentage, no_percentage) = self._get_voting_result_in_percentage();
            return (n_yes, n_no, yes_percentage, no_percentage);
        }

        /// @dev Check whether a voter is allowed to vote
        fn voter_can_vote(self: @ContractState, user_address: ContractAddress) -> bool {
            self.can_vote.read(user_address)
        }

        /// @dev Check whether an address is registered as a voter
        fn is_voter_registered(self: @ContractState, address: ContractAddress) -> bool {
            self.registered_voter.read(address)
        }

        /// @dev Submit a vote
        fn vote(ref self: ContractState, vote: u8) {
            assert(vote == NO || vote == YES, 'VOTE_0_OR_1');

            let caller: ContractAddress = get_caller_address();
            self._assert_allowed(caller);

            self.can_vote.write(caller, false);

            if (vote == NO) {
                self.no_votes.write(self.no_votes.read() + 1_u8);
            }
            if (vote == YES) {
                self.yes_votes.write(self.yes_votes.read() + 1_u8);
            }

            self.emit(VoteCast { voter: caller, vote: vote,  });
        }
    }

    /// @dev Internal Functions implementation for the Vote contract
    #[generate_trait]
    impl InternalFunctions of InternalFunctionsTrait {
        /// @dev Registers the voters and initializes their voting status to true (can vote)
        fn _register_voters(
            ref self: ContractState,
            voter_1: ContractAddress,
            voter_2: ContractAddress,
            voter_3: ContractAddress
        ) {
            self.registered_voter.write(voter_1, true);
            self.can_vote.write(voter_1, true);

            self.registered_voter.write(voter_2, true);
            self.can_vote.write(voter_2, true);

            self.registered_voter.write(voter_3, true);
            self.can_vote.write(voter_3, true);
        }
    }

    /// @dev Asserts implementation for the Vote contract
    #[generate_trait]
    impl AssertsImpl of AssertsTrait {
        // @dev Internal function that checks if an address is allowed to vote
        fn _assert_allowed(ref self: ContractState, address: ContractAddress) {
            let is_voter: bool = self.registered_voter.read((address));
            let can_vote: bool = self.can_vote.read((address));

            if (can_vote == false) {
                self.emit(UnauthorizedAttempt { unauthorized_address: address,  });
            }

            assert(is_voter == true, 'USER_NOT_REGISTERED');
            assert(can_vote == true, 'USER_ALREADY_VOTED');
        }
    }

    /// @dev Implement the VotingResultTrait for the Vote contract
    #[generate_trait]
    impl VoteResultFunctionsImpl of VoteResultFunctionsTrait {
        // @dev Internal function to get the voting results (yes and no vote counts)
        fn _get_voting_result(self: @ContractState) -> (u8, u8) {
            let n_yes: u8 = self.yes_votes.read();
            let n_no: u8 = self.no_votes.read();

            return (n_yes, n_no);
        }

        // @dev Internal function to calculate the voting results in percentage
        fn _get_voting_result_in_percentage(self: @ContractState) -> (u8, u8) {
            let n_yes: u8 = self.yes_votes.read();
            let n_no: u8 = self.no_votes.read();

            let total_votes: u8 = n_yes + n_no;

            let yes_percentage: u8 = (n_yes * 100_u8) / (total_votes);
            let no_percentage: u8 = (n_no * 100_u8) / (total_votes);

            return (yes_percentage, no_percentage);
        }
    }
}
----

Before we proceed with the Vote contract, let's get an understanding of types and imports.


[NOTE]
====
The Book is a community-driven effort created for the community.

* If you've learned something, or not, please take a moment to provide feedback through https://a.sprig.com/WTRtdlh2VUlja09lfnNpZDo4MTQyYTlmMy03NzdkLTQ0NDEtOTBiZC01ZjAyNDU0ZDgxMzU=[this 3-question survey].
* If you discover any errors or have additional suggestions, don't hesitate to open an https://github.com/starknet-edu/starknetbook/issues[issue on our GitHub repository].
====

include::ROOT:partial$snippet_contributing_blurb.adoc[leveloffset=+1]