[id="functions"]
= Functions and Traits in Cairo for Starknet

[NOTE]
====
The Chapters in the Starknet Book explain Cairo in the Starknet context. For a deeper understanding of Cairo not necessarily applied to Starknet, please refer to the https://cairo-book.github.io/[Cairo Book].
====

In Starknet, functions serve as modular blocks of logic, allowing for code structuring and reusability. They bear similarity to Solidity's functions and contracts but feature distinctive usage and implementation traits. 

Functions in Cairo are declared using the `fn` keyword within an implementation block, `impl`, or with the attribute `#[constructor]`. The functions' attributes may vary, they can be `#[constructor]`, `#[generate_trait]`, or `#[external(v0)]`, depending on their function. These functions can accept arguments, return values, and interact with the contract's storage. Moreover, the function execution context can be specified in the function signature using `self: @T` or `ref self: T`.

[NOTE]
====
In Cairo, function visibility is not explicitly specified. Functions are considered internal by default unless marked with the `#[external(v0)]` attribute. This is different from Solidity where visibility is defined using keywords (public, external, internal, or private).
====

The Vote contract, available in the https://github.com/starknet-edu/starknetbook/blob/main/chapters/book/modules/chapter_2/pages/contracts/src/vote.cairo[Starknet Book's repository], is used to illustrate different types of functions in Starknet. But first, let's take a look at the traits.

== Traits

Traits in Starknet are used to define a group of methods that may belong to different types. They are similar to interfaces in languages like Rust, Java, and TypeScript. In our Vote contract, the `VoteTrait` trait defines the set of external methods (more below) that the contract will implement.

[source,cairo]
----
#[starknet::interface]
trait VoteTrait<T> {
    fn get_vote_status(self: @T) -> (u8, u8, u8, u8);
    fn voter_can_vote(self: @T, user_address: ContractAddress) -> bool;
    fn is_voter_registered(self: @T, address: ContractAddress) -> bool;
    fn vote(ref self: T, vote: u8);
}
----

In the trait definition, the `T` parameter represents the contract's state. In each method signature, the context is specified using either `self: @T` (pass-by-reference context), for functions that only need to read the state `T`, or `ref self: T` (mutable pass-by-reference context), for functions that would modify the state, `ref self: T`, of the contract.

== Attributes in Cairo Functions

Attributes in Cairo functions are used to specify metadata about the functions, such as their visibility or special functionality. In Starknet, the used function attributes include:

- `#[constructor]`: This attribute marks a function that is used to initialize a contract's state upon deployment.
- `#[external(v0)]`: Functions with this attribute are callable externally, such as from user transactions or other contracts.
- `#[generate_trait]`: This attribute generates a trait based on the functions defined in the impl block. The generated trait can be used to group functions that have similar functionalities and will be internal; not callable from outside the contract.

== Constructor Functions

Constructor functions are defined using the `#[constructor]` attribute and are used to initialize the contract. They can only be executed once upon contract deployment. In the example contract, `constructor` sets up the initial state of the contract by registering voters and setting vote counts to 0. They can be defined directly in the contract without need of an implementation block.

In this case the constructor alters the state of the contract and so it receives the state of the contract, `ContractState`, as a mutable reference to it, `ref self: ContractState`. Then it receives the rest of the arguments that need to be defined when the contract is deployed to Starknet. Note the Contract State will be automatically feed to the constructor, that is, when the contract is deployed it does not need to be defined.

[source,cairo]
----
#[constructor]
fn constructor(
    ref self: ContractState,
    voter_1: ContractAddress,
    voter_2: ContractAddress,
    voter_3: ContractAddress
) {
    self._register_voters(voter_1, voter_2, voter_3);
    self.yes_votes.write(0_u8);
    self.no_votes.write(0_u8);
}
----

== External Functions

External functions are defined with the `#[external(v0)]` attribute and can be invoked within the same contact or by other contracts. In the Vote contract, there are several external functions defined, for instance, `get_vote_status` returns the current voting results, and `vote` accepts a vote from a registered and eligible voter. There are two main types of external functions, which are differentiated by how they can interact with the contract's state: read functions and write functions.

They should be defined in an impl block. The impl should be done for the trait that defined what external functions the contract would have.

[source,cairo]
----
#[external(v0)]
impl VoteImpl of super::VoteTrait<ContractState> {
    fn get_vote_status(self: @ContractState) -> (u8, u8, u8, u8) {
        // Implementation
    }

    fn vote(ref self: ContractState, vote: u8) {
        // Implementation
    }
}
----

=== Read Functions

Read functions in Starknet are functions that interact with a contract's storage without modifying it. They are commonly used to retrieve the contract's state information. This type of function is invoked with the `self: @T` notation, which provides the function with a reference (non-modifiable) to the contract's state. 

In our Vote contract, the function `get_vote_status` and `voter_can_vote` are examples of read functions. The `get_vote_status` function returns the number of yes votes, no votes, and their percentages. The `voter_can_vote` function checks if a particular voter, given by their address, is eligible to vote.

[source,cairo]
----
fn get_vote_status(self: @ContractState) -> (u8, u8, u8, u8) {
    let (n_yes, n_no) = self._get_voting_result();
    let (yes_percentage, no_percentage) = self._get_voting_result_in_percentage();
    return (n_yes, n_no, yes_percentage, no_percentage);
}

fn voter_can_vote(self: @ContractState, user_address: ContractAddress) -> bool {
    self.can_vote.read(user_address)
}
----

==== Write Functions

Write functions, in contrast, are functions that can modify the contract's storage. They can be invoked from an external context, such as a user transaction or a call from another contract, or an internal context. The `ref self: T` notation is used to provide the function with a mutable reference to the contract's state; that is, we can modify it.

The `vote` function in our Vote contract is an example of a write function. It updates the voting count based on the provided vote and sets the voter's eligibility to false.

[source,cairo]
----
fn vote(ref self: ContractState, vote: u8) {
    assert(vote == NO || vote == YES, 'VOTE_0_OR_1');

    let caller: ContractAddress = get_caller_address();
    self._assert_allowed(caller);

    self.can_vote.write(caller, false);

    if (vote == NO) {
        self.no_votes.write(self.no_votes.read() + 1_u8);
    }
    if (vote == YES) {
        self.yes_votes.write(self.yes_votes.read() + 1_u8);
    }
}
----

== Internal Functions

In addition to external functions, Starknet contracts can also define internal functions. Internal functions are the default type of functions in a Starknet contract. They can be invoked only from within the contract and are primarily used to perform specific computations or actions that are used by multiple external functions. The notion of internal functions matches closely with Solidity's internal visibility.

The signature of internal functions can use the **`self: @T`** or **`ref self: T`** notation, similar to external functions, depending on whether they need to read or write the contract's state.

For internal functions, we can use an `impl` block annotated with `#[generate_trait]`. The `#[generate_trait]` attribute generates a trait corresponding to the functions within the block.

[source, rust]
----
#[generate_trait]
impl InternalFunctions of InternalFunctionsTrait {
    // Implementation of the internal functions
}
----

We don't need to manually define a trait for these functions. The functions within this block can only be called by other functions within the contract. We can also use multiple **`impl`** blocks to better organize our internal functions. For instance, we could have separate **`impl`** blocks for vote validation, vote registration, and vote counting.

This pattern of using **`impl`** blocks allows us to structure our Starknet contracts in a clean and organized manner, with a clear separation between the different types of functions and their implementations.

Let's look at an example from our Vote contract. The **`_register_voters`** function is an internal function used to register voters and set their eligibility to true. Note that it can alter the contract's state.

[source,cairo]
----
fn _register_voters(ref self: ContractState, voter_1: ContractAddress, voter_2: ContractAddress, voter_3: ContractAddress) {
    self.can_vote.write(voter_1, true);
    self.can_vote.write(voter_2, true);
    self.can_vote.write(voter_3, true);
}
----

The **`_get_voting_result`** function is another internal function that calculates the total votes for 'yes' and 'no' options. It is a read function as it does not alter the contract's state.

[source,cairo]
----
fn _get_voting_result(self: @ContractState) -> (u8, u8) {
    return (self.yes_votes.read(), self.no_votes.read());
}
----


[NOTE]
====
The Book is a community-driven effort created for the community.

* If you've learned something, or not, please take a moment to provide feedback through https://a.sprig.com/WTRtdlh2VUlja09lfnNpZDo4MTQyYTlmMy03NzdkLTQ0NDEtOTBiZC01ZjAyNDU0ZDgxMzU=[this 3-question survey].
* If you discover any errors or have additional suggestions, don't hesitate to open an https://github.com/starknet-edu/starknetbook/issues[issue on our GitHub repository].
====

include::ROOT:partial$snippet_contributing_blurb.adoc[leveloffset=+1]
